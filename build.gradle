buildscript {
    apply from: gradle.dolbyio.files.modules
    apply from: "./gradle/iosSimulatorList.gradle"

    dependencies {
        classpath dolbyio.nexus.gradlePlugin
        classpath dolbyio.sonarqube.gradlePlugin
        classpath dolbyio.license.gradlePlugin
        classpath dolbyio.dokka.gradlePlugin
    }
}

plugins {
    alias(dolbyio.plugins.dokka)
    alias(dolbyio.plugins.publish.nexus)
    alias(dolbyio.plugins.kotlin.multiplatform) apply false
    alias(dolbyio.plugins.android.library) apply false
    alias(dolbyio.plugins.kotlin.jvm) apply false
    alias(dolbyio.plugins.multiplatform.moko.resources.generator) apply false
    alias(dolbyio.plugins.multiplatform.buildkonfig) apply false
}

group = "eu.codlab"
version = property("version")

ext.commit = 'git rev-parse --verify --short HEAD'.execute().text.trim()

subprojects {
    group = "eu.codlab"
    version = property("version")

    task allDeps(type: DependencyReportTask) {}

    if (isSourcesModule(it)) {
        apply from: gradle.dolbyio.files.moduleSetup
    }
}

/**
 * Definitions for the publication only
 */
def getExtraString = { String name ->
    if (rootProject.ext.has(name)) return rootProject.ext[name]?.toString()
    else return null
}

nexusPublishing {
    repositories {
        sonatype {
            username = getExtraString("sonatypeUsername") ?: ""
            password = getExtraString("sonatypePassword") ?: ""
        }
    }
}

tasks.register("quality") { qualityTask ->
    group = "verification"

    subprojects.each { project ->
        def tasks = ["ktlint", "detekt"].collect {
            project.tasks.named(it)
        }.findAll { it != null }

        qualityTask.dependsOn(tasks)
    }
}